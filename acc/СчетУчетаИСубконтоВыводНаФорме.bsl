// Данный код рассчитан на то, что в объекте есть реквизиты СчетУчетаРасчетов и Субконто1, Субконто2, Субконто3 
// Реквизиты выведены на форму с такими именами элементов

// Реквизиты объекта Субконто1, Субконто2, Субконто3 должны иметь связь по типу с реквизитом СчетучетаРасчетов 
// Номера элементов связи 1, 2, 3 соответственно

&НаКлиенте
Процедура СчетУчетаРасчетовПриИзменении(Элемент)
	
	// метод вынесен на сервер, чтобы можно было его вызвать при создании формы
	// если данного вызова не требуется можно оставить вызов на клиенте
	УстановитьСвойстваСубконто();
	
КонецПроцедуры
	
&НаСервере
Процедура УстановитьСвойстваСубконто()
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСчета(
		ЭтаФорма, Объект, ПараметрыУстановкиСвойствСубконто());
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	// Данный метод обслуживает обработку начало выбора всех трех субконто

	ДанныеОбъекта = БухгалтерскийУчетКлиентСервер.ДанныеУстановкиПараметровСубконто(Объект, 
		ПараметрыУстановкиСвойствСубконто());
	
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтаФорма, Элемент, СтандартнаяОбработка, ДанныеОбъекта);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыУстановкиСвойствСубконто()
	
	Поля = Новый Структура("Субконто1, Субконто2, Субконто3, СчетУчета, Подразделение", 
		"Субконто1", "Субконто2", "Субконто3", "СчетУчетаРасчетов", "Подразделение");
	
	ПараметрыУстановки = Новый Структура();
	ПараметрыУстановки.Вставить("ПоляФормы",  	 		Поля);
	ПараметрыУстановки.Вставить("ПоляОбъекта", 			Поля);
	ПараметрыУстановки.Вставить("СкрыватьСубконто", 	Истина);
	ПараметрыУстановки.Вставить("ДопРеквизиты", 		Новый Структура);
	ПараметрыУстановки.Вставить("ЗначенияПоУмолчанию", 	Новый Структура);
	
	Возврат ПараметрыУстановки;
	
КонецФункции
