// Возвращает массив фиксированных массивов всевозможных перестановок элементов. Полезна при определения комбинаций сумм.
//
// Параметры:
//  МассивЭлементов   - массив - массив произвольных элементов, образующих комбинации. Произвольный.
//  ДлинаПерестановки - число - количество элементов в комбинации. Целое.
//  БезПовторов       - булево - признак необходимости получать в результате комбинации 
//                      в которых один и тот же элемент мЭлементов встречался бы не чаще 1 раза. По-умолчанию - Ложь.
Функция Перестановки(МассивЭлементов, ДлинаПерестановки, БезПовторов, 
            Основание = Неопределено, мРезультата = Неопределено, ТекУровень = 0) Экспорт
    
    Если Основание = Неопределено Тогда 
        Основание = Новый Массив; 
    КонецЕсли;

    Если мРезультата = Неопределено Тогда 
        мРезультата = Новый Массив; 
    КонецЕсли;
        
    Для Каждого Элемент Из МассивЭлементов Цикл
        
        Если БезПовторов И НЕ Основание.Найти(Элемент) = Неопределено Тогда
            Продолжить;
        КонецЕсли;

        Основание.Добавить(Элемент);

        Если ТекУровень < ДлинаПерестановки - 1 Тогда

            мРезультата = Перестановки(МассивЭлементов, ДлинаПерестановки, 
                БезПовторов, Основание, мРезультата, ТекУровень + 1);    
        Иначе
            мРезультата.Добавить(Новый ФиксированныйМассив(Основание));
        
        КонецЕсли;
        
        Основание.Удалить(Основание.Количество() - 1);

    КонецЦикла;  
    
    Возврат мРезультата; 

КонецФункции
