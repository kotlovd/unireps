// мЭлементов        - массив произвольных элементов, образующих комбинации. Произвольный.
// линаПерестановки  - количество элементов в комбинации. Целое.
// БезПовторов       - признак необходимости получать в результате комбинации 
//                     в которых один и тот же элемент мЭлементов встречался бы не чаще 1 раза. 
//                     Булево. По-умолчанию - Ложь.

Функция Перестановки(мЭлементов, ДлинаПерестановки, БезПовторов, 
            Основание = Неопределено, мРезультата = Неопределено, ТекУровень = 0) Экспорт
    
    Если Основание = Неопределено Тогда 
        Основание = Новый Массив; 
    КонецЕсли;

    Если мРезультата = Неопределено Тогда 
        мРезультата = Новый Массив; 
    КонецЕсли;
        
    Для Каждого Элемент Из мЭлементов Цикл
        
        Если БезПовторов И НЕ Основание.Найти(Элемент) = Неопределено Тогда
            Продолжить;
        КонецЕсли;

        Основание.Добавить(Элемент);
        Если ТекУровень < ДлинаПерестановки - 1 Тогда
            мРезультата = Перестановки(мЭлементов, ДлинаПерестановки, 
                БезПовторов, Основание, мРезультата, ТекУровень + 1);    
        Иначе
            мРезультата.Добавить(Новый ФиксированныйМассив(Основание));
        КонецЕсли;
        Основание.Удалить(Основание.Количество() - 1);

    КонецЦикла;  
    
    Возврат мРезультата; 

КонецФункции
