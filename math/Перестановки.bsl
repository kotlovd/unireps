//мЭлементов         - массив произвольных элементов, образующих комбинации. Произвольный.
//ДлинаПерестановки    - количество элементов в комбинации. Целое.
//БезПовторов        - признак необходимости получать в результате комбинации в которых один и тот же элемент мЭлементов встречался бы не чаще 1 раза. Булево. По умолчанию - Ложь.

Функция Перестановки(мЭлементов, ДлинаПерестановки, БезПовторов = Ложь, Основание = Неопределено, мРезультата = Неопределено, ТекУровень = 0) Экспорт
    
    Если Основание = Неопределено Тогда 
        Основание = Новый Массив 
    КонецЕсли;

    Если мРезультата = Неопределено Тогда 
        мРезультата = Новый Массив 
    КонецЕсли;
    
    Если ТекУровень < ДлинаПерестановки - 1 Тогда        
       
        Для Каждого Элемент Из мЭлементов Цикл    
            
            Если БезПовторов И НЕ Основание.Найти(Элемент) = Неопределено Тогда
            
            Иначе
                
                Основание.Добавить(Элемент);
                мРезультата = Перестановки(мЭлементов, ДлинаПерестановки, БезПовторов, Основание, мРезультата, ТекУровень + 1);
                Основание.Удалить(Основание.Количество() - 1);
                
            КонецЕсли;

        КонецЦикла;

    Иначе
        
        Для Каждого Элемент Из мЭлементов Цикл
            
            Если БезПовторов И НЕ Основание.Найти(Элемент) = Неопределено Тогда // BSLLS:EmptyCodeBlock-off
            
            Иначе

                Основание.Добавить(Элемент);
                мРезультата.Добавить(Новый ФиксированныйМассив(Основание));
                Основание.Удалить(Основание.Количество() - 1);

            КонецЕсли;

        КонецЦикла;

    КонецЕсли;   
    
    Возврат мРезультата; 

КонецФункции
