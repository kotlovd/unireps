&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	
	//Добавление команды формы
	Команда = Команды.Добавить(
		"ПоказатьПредупреждение"); //Имя команды
	Команда.Заголовок = "Показать предупреждение";
	Команда.Действие  = "КомандаПоказатьПредупреждение"; //Имя связанной процедуры
	
	//Полный перечень свойств команды в синтакс-помощнике
	//	Интерфейс (управляемый)->Форма клиентского приложения->КомандаФормы
	
	//Добавление кнопки формы
	КнопкаФормы = Элементы.Добавить(
		"КнопкаПоказатьПредупреждение", //Имя кнопки
		Тип("КнопкаФормы"),             //Тип, всегда КнопкаФормы
		ЭтаФорма);                      //Контейнер для кнопки (Форма, Группа, Командная панель)
		
	КнопкаФормы.ИмяКоманды = "ПоказатьПредупреждение"; //Связь с командой по имени
	
	//Возможные варианты вида кнопки:
	//	-Гиперссылка
	//	-ГиперссылкаКоманднойПанели
	//	-КнопкаКоманднойПанели
	//	-ОбычнаяКнопка
	//Если контейнер для формы - командная панель
	//то вид кнопки должен быть КнопкаКоманднойПанели
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка; 
	
	//Полный перечень свойств кнопки формы в синтакс-помощнике
	//	Интерфейс (управляемый)->Кнопка формы
	
	// Сокращенный вариант
	ИмяКоманды       = "ск_ВывестиСтатичныйМакет";
	ЗаголовокКоманды = "Вывести статичный макет";

	Команда = Команды.Добавить(ИмяКоманды);
	Команда.Заголовок = ЗаголовокКоманды;
	Команда.Действие  = "Команда" + ИмяКоманды; // имя процедуры обработчика команды

	КнопкаФормы = Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), Элементы.ГруппаВывод);	
	КнопкаФормы.ИмяКоманды = ИмяКоманды;
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	
КонецПроцедуры

//Процедура вызываемая командой "ПоказатьПредупреждение"
&НаКлиенте
Процедура КомандаПоказатьПредупреждение(Команда)
	
	ПоказатьПредупреждение(, "ОК!");
	
КонецПроцедуры
