// 1. Предполагается, что на форме есть реквизит ТаблицаДокументов. Реквизиты таблицы должны совпадать по имени с полями СКД 
// 2. Предполагает, что на форме есть реквизит КомпоновщикНастроекКД - компоновщик настроек компоновки данных
// 3. На форме надо разместить таблицу отбора из настроек компоновщика

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СхемаКД     = Документы.РеализацияТоваровУслуг.ПолучитьМакет("ОтборРеализацийДляПечати");
	URLСхемыКД  = ПоместитьВоВременноеХранилище(СхемаКД, УникальныйИдентификатор);//URL нужен для тонких форм, в обычных передается сама схема
	
	ИсточникДоступныхНастроекКД = Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемыКД);
	КомпоновщикНастроекКД.Инициализировать(ИсточникДоступныхНастроекКД);
	
	КомпоновщикНастроекКД.ЗагрузитьНастройки(СхемаКД.НастройкиПоУмолчанию);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписок(Команда)
	
	Если НЕ ЗначениеЗаполнено(Период.ДатаНачала) ИЛИ НЕ ЗначениеЗаполнено(Период.ДатаОкончания) Тогда
		
		ПоказатьПредупреждение(, "Не указан период отбора документов");
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьСписокНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокНаСервере()
	
	СхемаКД = Документы.РеализацияТоваровУслуг.ПолучитьМакет("ОтборРеализацийДляПечати"); // макет СКД, указать свой
	
	КомпоновщикМакетаКД = Новый КомпоновщикМакетаКомпоновкиДанных;
	НастройкиКД = КомпоновщикНастроекКД.ПолучитьНастройки();
	
	НастройкиКД.ПараметрыДанных.УстановитьЗначениеПараметра("НачалоПериода", Период.ДатаНачала);
	НастройкиКД.ПараметрыДанных.УстановитьЗначениеПараметра("КонецПериода", КонецДня(Период.ДатаОкончания));
	
	МакетКД = КомпоновщикМакетаКД.Выполнить(СхемаКД, НастройкиКД,,,
		Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// Создаем Процесор КД
	ПроцессорКД = Новый ПроцессорКомпоновкиДанных;
	
	ПроцессорКД.Инициализировать(МакетКД,,,Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.ОтображатьПроцентВывода = Истина;
	
	ТаблицаДокументов.Очистить();
	ТЗ = Новый ТаблицаЗначений;
	
	ПроцессорВывода.УстановитьОбъект(ТЗ);
	ПроцессорВывода.Вывести(ПроцессорКД, Истина);
	
	ТаблицаДокументов.Загрузить(ТЗ);
	
КонецПроцедуры
